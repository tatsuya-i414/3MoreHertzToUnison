; ------------------------------------------------------------
; 演出用マクロ
; ------------------------------------------------------------
; 画面暗転
[macro name="blackout"]
    [mask effect="fadeIn" time="%time_1|3000"]
    [if exp="%exp"]
        ; 背景パーツを表示
        [if exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode1' " ]
            [if exp="f.isLightCoverGet == 0"]
                [image storage="../image/episode1/lightcover_item.png" layer="1" x="450" y="770" name="lightcover"]
            [endif]
            [if exp="f.isSpeakerStatusGreen == 1"]
                [image storage="../image/episode1/speaker_afterrepair.png" layer="1" x="1" y="125" name="speaker"]
            [else]
                [image storage="../image/episode1/speaker_beforerepair.png" layer="1" x="1" y="110" name="speaker"]
            [endif]
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode2' "]
            [if exp="f.isRedBlockGet == 0 && f.isBlueBlockGet == 0 && f.isGreenBlockGet == 0"]
                [image storage="../image/episode2/block.png" layer="1" x="265" y="527" name="block"]
            [endif]
            [if exp="f.isHangerGet == 0"]
                [image storage="../image/episode2/hanger.png" layer="1" x="148" y="410" name="hanger"]
            [endif]
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode2_dress' "]
            ; 追加予定
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode3' "]
            [if exp="f.isJacketGet == 0"]
                [image storage="../image/episode3/jacket.png" layer="1" x="1150" y="750" name="jacket"]
            [endif]
            [if exp="f.isJacketGet == -1 && f.isRoomLightNight == 0"]
                [image storage="../image/episode3/wallhanger_onjacket.png" layer="1" x="1427" y="90" name="wallhanger_onjacket"]
            [elsif exp="f.isJacketGet == -1 && f.isRoomLightNight == 1"]
                [image storage="../image/episode3/wallhanger_onjacket_night.png" layer="1" x="1411" y="107" name="wallhanger_onjacket_night"]
            [else]
                [image storage="../image/episode3/wallhanger_onhanger.png" layer="1" x="1414" y="111" name="wallhanger_onhanger"]
            [endif]
            [if exp="f.isJutanOpen == 1"]
                [image storage="../image/episode3/jutan_turnedup.png" layer="1" x="585" y="908" name="jutan_turndup"]
                [if exp="f.isRoomLightNight == 1"]
                    [image storage="../image/episode3/mark_underjutan.png" layer="1" x="1175" y="915" name="mark"]
                [endif]
            [else]
                [image storage="../image/episode3/jutan.png" layer="1" x="585" y="908" name="jutan"]
            [endif]
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode3_jacket' "]
            [free layer="1" name="wallhanger_onhanger"]
            [if exp="f.isRoomLightNight == 0"]
                [image storage="../image/episode3/wallhanger_onjacket.png" layer="1" x="1427" y="90" name="wallhanger_onjacket"]
            [elsif exp="f.isRoomLightNight == 1"]
                [image storage="../image/episode3/wallhanger_onjacket_night.png" layer="1" x="1411" y="107" name="wallhanger_onjacket_night"]
            [endif]
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'episode3_bed' "]
            [free layer="1" name="wallhanger_onjacket"]
            [image storage="../image/episode3/wallhanger_onjacket_night.png" layer="1" x="1411" y="107" name="wallhanger_onjacket_night"]
            [if exp="f.isJutanOpen == 1"]
                [image storage="../image/episode3/mark_underjutan.png" layer="1" x="1175" y="915" name="mark"]
            [endif]
        [elsif exp="f.itemVisible[0] == 'true' && f.itemVisible[1] == 'epilogue_true' "]
            [layer1True]
            [layer3True]
        [endif]
        [bg storage="%storage_1" time="%time_2|100"]
    [else]
        [bg storage="%storage_2" time="%time_3|100"]
    [endif]
    [mask_off time="2000"]
[endmacro]

; 背景画面を暗くする
[macro name="DarkenBackground"]
    [filter layer="base" brightness="%brightness|15" blur="10"]
    [filter layer="1" brightness="%brightness|15" blur="10"]
[endmacro]

; アイテム小画面表示
[macro name="ItemDisp"]
    [layer2True]
    [DarkenBackground]
    [image storage="%storage" layer="2" x="780" y="350" width="250" height="250" name="itemDisp"]
[endmacro]

; アイテム小画面表示を解除
[macro name="FreeItemDisp"]
    [free layer="2" name="itemDisp"]
    [layer2False]
    [free_filter]
[endmacro]

; フラッシュ
[macro name="flasheffect"]
    [mask effect="fadeIn" color="%color|0xF5F5F5" graphic="%graphic" folder="image" time="%intime|80"]
    [mask_off effect="fadeOut" time="%outtime|160"]
[endmacro]
[return]